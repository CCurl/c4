find fill loaded?

: p1 vhere ->vars $100 + ;    : p2 p1 $100 + ;
: fill ( start ch num-- ) >t swap t>
    ?dup if for over over c! 1+ next then
    drop drop ;
: fill-c ( start n num-- ) >t swap t>
    ?dup if for over over ! cell + next then
    drop drop ;
: cmove ( src dst n-- ) +regs >a >d >s
    a> ?dup if for s>+ c@ d>+ c! next then
    -regs ;
: cmove> ( src dst n-- ) +regs >a a> + >d a> + >s
    a> ?dup if 1+ for s>- c@ d>- c! next then
    -regs ;

: s-trunc ( dst--dst )     0 over w! ;
: s-cat   ( dst src--dst ) >t >r r@ count + t@ 1+ s-cpy drop t> c@ r@ c+! r> ;
: s-catc  ( dst ch--dst )  over count + w! dup c++ ;
: s-catn  ( dst n--dst )   to-string s-cat ;
: s-scat  ( src dst--dst ) swap s-cat ;
: s-scatc ( ch dst--dst )  swap s-catc ;
: s-scatn ( n dst--dst )   swap s-catn ;

: sz-cat   ( dst src--dst )  >t >r r@ dup sz-len + t> s-cpy drop r> ;
: sz-rtrim ( sz--sz )  >t +regs t@ dup sz-len + 1- >a
    begin
        a> t@ < if t> -exit then
        a> c@ 32 > if t> -exit then
        0 a>- c!
    again ;
