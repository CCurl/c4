: replace-char ( ch-- ) printable? if rc>pos c! dirty mv-rt then show! ;
: insert-char  ( ch-- ) printable? 0= if exit then
    +regs >a rc>pos >x  row max-col >pos >y
    begin  y> 1- c@  y>- c!  y> x> > while
    a> x> c! -regs mv-rt dirty ;
: delete-char ( -- ) +regs rc>pos >x  row max-col >pos >y
    begin  x> 1+ c@  x>+ c!  x> y> < while
    32 y> 1- c! -regs dirty ;
: eol-offset ( row--off ) +regs >y max-col >x
    begin y> x> >pos c@ 32 > if x> 1+ -exit then x>- while 0 -regs ;
: mv-eol row eol-offset >col ;
: clear-line ( r-- ) 0 >pos max-col for 32 over c! 1+ next 0 swap c! dirty ;
: insert-line ( -- ) +regs
    row max-row < if
        row 0 >pos >s    s> cols + >d    max-row 0 >pos s> - >x
        s> d> x> cmove>
    then row clear-line
    dirty -regs ;
: delete-line ( -- ) +regs
    row max-row < if
        row 0 >pos >d    d> cols + >s    rows 0 >pos s> - >x
        s> d> x> cmove
    then   max-row clear-line
    dirty -regs ;
: insert-row ( r-- ) row swap  >row insert-line  >row ;
: delete-row ( r-- ) row swap  >row delete-line  >row ;
: join-lines ( -- ) row max-row >= if exit then
    p1 row 0 >pos s-cpy sz-rtrim
    row 1+ 0 >pos sz-cat cols 1- + 0 swap c!
    row 0 >pos p1 s-cpy drop
    row 1+ delete-row  ;

