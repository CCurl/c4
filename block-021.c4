: block-fn ( --fn ) p1 z" block-" s-cpy blk 10 /mod 10 /mod
    3 for p1 swap '0' + s-catc drop next
    z" .c4" s-cat ;
: w1 ( ch-- ) dup 10 = if row++ 0 >col rc>pos t! exit then 32 max !t+ ;
: w2 atdrop nt-lines ;
: work->block work >a  block >t  0 dup >row >col
    begin  @a+ ?dup  if0 w2 exit then  w1  row rows < while w2 ;
: clear-block 32 block-sz fill ;
: ed-load  work clear-block  block clear-block
    block-fn fopen-rb ?dup if
        >t work block-sz t@ fread drop t> fclose
    then work->block clean show! 0 0 ->edRC ;
: normal-mode  0 ;  : insert-mode  1 ;  : replace-mode  2 ;  : quit-mode 99 ;
: normal-mode?  normal-mode  ed-mode = ;  : normal-mode!  normal-mode  >ed-mode ;
: insert-mode?  insert-mode  ed-mode = ;  : insert-mode!  insert-mode  >ed-mode ;
: replace-mode? replace-mode ed-mode = ;  : replace-mode! replace-mode >ed-mode ;
: quit?         quit-mode    ed-mode = ;  : quit!         quit-mode    >ed-mode ;
: .mode space normal-mode? if ." -normal-" exit then
    red insert-mode?  if ." -insert-" then
    replace-mode? if ." -replace-" then
    white ;
: insert-toggle
    normal-mode? if insert-mode!  exit then
    insert-mode? if replace-mode! exit then
    normal-mode! ;






